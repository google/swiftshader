# Copyright 2016 The SwiftShader Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/toolchain/toolchain.gni")

# Need a separate config to ensure the warnings are added to the end.
config("swiftshader_libEGL_private_config") {
  defines = [ "EGL_EGLEXT_PROTOTYPES" ]

  if (is_win) {
    cflags = [
      "/wd4201",  # nameless struct/union
      "/wd4065",  # switch statement contains 'default' but no 'case' labels
      "/wd5030",  # attribute is not recognized
    ]

    defines += [
      "EGLAPI=",
      "LIBEGL_EXPORTS",
    ]
  } else {
    cflags = [ "-Wno-sign-compare" ]
    if (!is_clang) {
      cflags += [ "-Wno-unused-but-set-variable" ]
    }

    defines += [ "LOG_TAG=\"swiftshader_libEGL\"" ]

    if (is_linux) {
      defines += [ "EGLAPI=__attribute__((visibility(\"protected\")))" ]
    } else {
      defines += [ "EGLAPI=__attribute__((visibility(\"default\")))" ]
    }
  }
}

shared_library("swiftshader_libEGL") {
  if (!is_mac) {
    output_name = "libEGL"
    output_dir = "$root_out_dir/swiftshader"
  }

  sources = [
    "../common/Object.cpp",
    "Config.cpp",
    "Display.cpp",
    "EGLSurface.cpp",
    "libEGL.cpp",
    "libEGL.def",
    "libEGL.rc",
    "main.cpp",
    "resource.h",
  ]

  if (is_debug) {
    sources += [ "../common/debug.cpp" ]
  }

  if (is_mac) {
    sources += [ "OSXUtils.mm" ]
    libs = [
      "Quartz.framework",
      "Cocoa.framework",
    ]
  } else if (is_win) {
    configs -= [ "//build/config/win:unicode" ]
    ldflags = [ "/DEF:" + rebase_path("libGLESv2.def", root_build_dir) ]
  } else if (is_linux) {
    sources += [
      "../../Main/libX11.cpp",
      "TypeInfo.cpp",
    ]
    ldflags =
        [ "-Wl,--version-script=" + rebase_path("exports.map", root_build_dir) ]
  }

  if (use_thin_lto) {
    # https://crbug.com/686980
    # This target relies on the linker to be smart and garbage collect unused symbols.
    # It is not the case for LLD + ThinLTO, so we have to use this ugly workaround.
    cflags = [
      "-Xclang",
      "-fno-lto-unit",
      "-fno-whole-program-vtables",
      "-fno-sanitize=cfi",
    ]
  }

  configs -= [ "//build/config/compiler:chromium_code" ]
  configs += [
    "//build/config/compiler:no_chromium_code",
    "//third_party/swiftshader:swiftshader_config",
    ":swiftshader_libEGL_private_config",
  ]

  include_dirs = [
    "../../../include",
    "../..",
    "..",
  ]
}
