# Copyright 2016 The SwiftShader Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Need a separate config to ensure the warnings are added to the end.
config("swiftshader_libGLESv2_private_config") {
  if (is_win) {
    cflags = [
      "/DGL_API=",
      "/DGL_APICALL=",
      "/DGL_GLEXT_PROTOTYPES",
      "/DLIBGLESV2_EXPORTS",
      "/wd4201", # nameless struct/union
      "/wd4324", # structure was padded due to alignment specifier
    ]

    if (is_clang)
    {
      cflags += [
        "-D__STDC_CONSTANT_MACROS",
        "-D__STDC_LIMIT_MACROS",
      ]
    }
  } else {
    cflags = [
      "-DLOG_TAG=\"swiftshader_libGLESv2\"",
      "-D__STDC_CONSTANT_MACROS",
      "-D__STDC_LIMIT_MACROS",
      "-DGL_API=",
      "-DGL_APICALL=",
      "-DGL_GLEXT_PROTOTYPES",
      "-Wno-sign-compare",
    ]
  }
}

shared_library("swiftshader_libGLESv2") {
  if (!is_mac) {
    output_name = "libGLESv2"
    output_dir = "$root_out_dir/swiftshader"
  }

  deps = [
    "../../OpenGL/compiler:swiftshader_opengl_compiler",
    "../../Reactor:swiftshader_reactor",
    "../../Renderer:swiftshader_renderer",
  ]

  sources = [
    "Buffer.cpp",
    "Context.cpp",
    "Device.cpp",
    "Fence.cpp",
    "Framebuffer.cpp",
    "IndexDataManager.cpp",
    "libGLESv2.cpp",
    "libGLESv3.cpp",
    "main.cpp",
    "Program.cpp",
    "Query.cpp",
    "Renderbuffer.cpp",
    "ResourceManager.cpp",
    "Shader.cpp",
    "Texture.cpp",
    "TransformFeedback.cpp",
    "utilities.cpp",
    "VertexArray.cpp",
    "VertexDataManager.cpp",
    "libGLESv2.def",
    "libGLESv2.rc",
    "resource.h",
  ]

  if (is_win) {
    configs -= [ "//build/config/win:unicode" ]
    ldflags = [ "/DEF:" + rebase_path("libGLESv2.def", root_build_dir) ]
  }

  configs -= [ "//build/config/compiler:chromium_code" ]
  configs += [
    "//build/config/compiler:no_chromium_code",
    "//third_party/swiftshader:swiftshader_config",
    ":swiftshader_libGLESv2_private_config",
  ]

  include_dirs = [
    "../../../include",
    "../..",
    "..",
  ]
}