# Copyright 2016 The SwiftShader Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Need a separate config to ensure the warnings are added to the end.
config("swiftshader_reactor_private_config") {
  if (is_win) {
    cflags = [
      "/wd4201", # nameless struct/union
      "/wd4245", # conversion from int to unsigned int (llvm)
    ]
  } else {
    cflags = [
      "-DLOG_TAG=\"swiftshader_reactor\"",
      "-Wno-unused-local-typedef",
      "-msse2",
      "-D__STDC_CONSTANT_MACROS",
      "-D__STDC_LIMIT_MACROS",
    ]
  }
}

source_set("swiftshader_reactor") {
  deps = [
    "../OpenGL/common:swiftshader_opengl_common",
    "../../third_party/LLVM:swiftshader_llvm",
  ]

  sources = [
    "LLVMReactor.cpp",
    "Routine.cpp",
    "LLVMRoutine.cpp",
    "LLVMRoutineManager.cpp",
  ]

  if (is_win) {
    configs -= [ "//build/config/win:unicode" ]
  }

  configs += [ ":swiftshader_reactor_private_config" ]

  include_dirs = [
    "..",
    "../Common",
    "../../third_party/LLVM/include/",
  ]
}